
# cpanel - Cpanel/ImagePrep/Task/roundcube.pm      Copyright 2022 cPanel, L.L.C.
#                                                           All rights reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited

package Cpanel::ImagePrep::Task::roundcube;

use cPstrict;

use parent 'Cpanel::ImagePrep::Task';
use Cpanel::FileUtils::Modify ();
use Cpanel::Slurper           ();
use Cpanel::UUID              ();

use Try::Tiny;

=head1 NAME

Cpanel::ImagePrep::Task::roundcube - An implementation subclass of Cpanel::ImagePrep::Task. See parent class for interface.

=cut

sub _description {
    return <<EOF;
Clear and regenerate the Roundcube calendar_crypt_key.
EOF
}

use constant CALENDAR_PLUGIN_DIR => '/usr/local/cpanel/base/3rdparty/roundcube/plugins/calendar';
use constant CALENDAR_CONF_FILE  => CALENDAR_PLUGIN_DIR() . '/config.inc.php';
use constant CALENDAR_KEY_FILE   => CALENDAR_PLUGIN_DIR() . '/calendar_crypt_key.inc';
use constant FILE_PERMS          => 0644;

sub _type { return 'non-repair only' }

sub _pre ($self) {

    if ( !$self->common()->_exists( CALENDAR_KEY_FILE() ) ) {
        $self->loginfo('Roundcube calendar_crypt_key.inc does not exist.');
        return $self->PRE_POST_NOT_APPLICABLE;
    }

    if ( !$self->common()->_unlink( CALENDAR_KEY_FILE() ) ) {
        $self->loginfo('Failed to remove the Roundcube calendar_crypt_key.');
        return $self->PRE_POST_FAILED;
    }

    # Replacing the calendar_crypt_key config value is not critical to the
    # process because it only ingests the contents of CALENDAR_KEY_FILE(). This
    # is some friendly sabotage to make it apparent if post_snapshot does not
    # succeed.
    Cpanel::FileUtils::Modify::match_replace(
        CALENDAR_CONF_FILE(),
        [
            {
                match   => qr{^\$config\['calendar_crypt_key'] = .*$}m,
                replace => qq{\$config['calendar_crypt_key'] = null; throw new Exception('configuration must be regenerated by running post_snapshot or by removing this configuration file and reinstalling the cpanel-roundcube package.');}
            },
        ]
    );

    $self->loginfo('Cleared roundcube calendar_crypt_key.');
    return $self->PRE_POST_OK;
}

sub _post ($self) {

    if ( !$self->common->_exists( CALENDAR_PLUGIN_DIR() ) ) {
        $self->loginfo('The Roundcube calendar plugin is not installed.');
        return $self->PRE_POST_NOT_APPLICABLE;
    }

    # Using a random UUID here to stay consistent with what the
    # cpanel-roundcube package uses.
    my $write_ok = try {
        Cpanel::Slurper::write( CALENDAR_KEY_FILE(), Cpanel::UUID::random_uuid(), FILE_PERMS() );
        1;
    }
    catch {
        $self->loginfo("Failed to regenerate the Roundcube calendar_crypt_key: $_");
        0;
    };
    return $self->PRE_POST_FAILED unless $write_ok;

    Cpanel::FileUtils::Modify::match_replace(
        CALENDAR_CONF_FILE(),
        [
            {
                match   => qr{^\$config\['calendar_crypt_key'] = .*$}m,
                replace => q{$config['calendar_crypt_key'] = file_get_contents('} . CALENDAR_KEY_FILE() . q{'); /* updated by post_snapshot */},
            },
        ]
    );

    $self->loginfo('Regenerated the Roundcube calendar_crypt_key.');

    return $self->PRE_POST_OK;
}

1;
