
# cpanel - Cpanel/ImagePrep/Task/phpmyadmin.pm     Copyright 2022 cPanel, L.L.C.
#                                                           All rights reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited

package Cpanel::ImagePrep::Task::phpmyadmin;

use cPstrict;

use parent 'Cpanel::ImagePrep::Task';
use Cpanel::FileUtils::Modify ();
use Cpanel::Rand::Get         ();

=head1 NAME

Cpanel::ImagePrep::Task::phpmyadmin - An implementation subclass of Cpanel::ImagePrep::Task. See parent class for interface.

=cut

sub _description {
    return <<EOF;
Clear and regenerate phpMyAdmin blowfish_secret value in config.inc.php.
EOF
}

use constant CONF => '/usr/local/cpanel/base/3rdparty/phpMyAdmin/config.inc.php';

sub _type { return 'non-repair only' }

sub _pre ($self) {

    if ( !$self->common->_exists( CONF() ) ) {
        $self->loginfo('phpMyAdmin config.inc.php does not exist.');
        return $self->PRE_POST_NOT_APPLICABLE;
    }

    my $updated = Cpanel::FileUtils::Modify::match_replace(
        CONF(),
        [
            {
                match   => qr/^\$cfg\['blowfish_secret'] = .*$/m,
                replace => qq{\$cfg['blowfish_secret'] = null; throw new Exception('configuration must be regenerated by running post_snapshot or /usr/local/cpanel/bin/update_phpmyadmin_config --force');}
            },
        ]
    );

    if ( !$updated ) {
        $self->loginfo('There is no blowfish_secret in the conf file.');
        return $self->PRE_POST_NOT_APPLICABLE;
    }

    $self->loginfo('Cleared phpMyAdmin blowfish_secret.');
    return $self->PRE_POST_OK;
}

sub _post ($self) {

    if ( !$self->common->_exists( CONF() ) ) {
        $self->loginfo('phpMyAdmin config.inc.php does not exist.');
        return $self->PRE_POST_NOT_APPLICABLE;
    }

    my $new_secret = Cpanel::Rand::Get::getranddata(32);
    my $updated    = Cpanel::FileUtils::Modify::match_replace(
        CONF(),
        [
            {
                match   => qr/^\$cfg\['blowfish_secret'] = .*$/m,
                replace => qq{\$cfg['blowfish_secret'] = '$new_secret'; /* updated by post_snapshot */},
            },
        ]
    );

    $self->loginfo('Regenerated phpMyAdmin blowfish_secret.');

    return $self->PRE_POST_OK;
}

1;
